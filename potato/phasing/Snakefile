configfile:'config.json'
alignments=config['alignments']
whatshap = config['whatshap']
snpeff = config['snpeff']
reference=config['reference']

chromosomes = ['chr01','chr02','chr03','chr04','chr05','chr06','chr07','chr08','chr09','chr10','chr11','chr12','chr00']

rule all:
	input:
		expand('vcf/variants.{chrom}.vcf', chrom=chromosomes),
		expand('phasing/polyphase/{technology}.{chrom}.polyphase-{mode}.vcf.gz', chrom=chromosomes, technology=['correctedminion'], mode=['verified-genotypes', 'all-genotypes']),
		expand('genes/stats/{technology}.polyphase-{mode}.tsv', technology=['correctedminion'], mode=['verified-genotypes', 'all-genotypes']),
		expand('genes/plots/{technology}.polyphase-{mode}-block_lengths.pdf', technology=['correctedminion'], mode=['verified-genotypes', 'all-genotypes']),
		expand('haplotag/tagged/{technology}.{mode}.tagged.{chrom}.bam.bai', technology=['correctedminion'], mode=['verified-genotypes'], chrom=chromosomes),
		expand('genes/annotation/{technology}.polyphase-{mode}_annotated.vcf', technology=['correctedminion'], mode=['verified-genotypes']),
		expand('assembly/split/{technology}.{mode}.{chrom}.h{hap}.bam', technology=['correctedminion'], mode=['verified-genotypes'], chrom=['chr04'], hap=[1,2,3,4]),
		expand('assembly/contigs/{technology}.{mode}.{chrom}.wtdbg2-h{hap}.raw.fa', technology=['correctedminion'], mode=['verified-genotypes'], chrom=['chr04'], hap=[1,2,3,4]),
		expand('assembly/contigs/{technology}.{mode}.{chrom}.wtdbg2-h{hap}.raw.bam.bai', technology=['correctedminion'], mode=['verified-genotypes'], chrom=['chr04'], hap=[1,2,3,4])


# index the bam files
rule index_bam:
	input:
		'{file}.bam'
	output:
		'{file}.bam.bai'
	shell:
		'samtools index {input}'

# download gene annotations
rule download_genes:
	output:
		'genes/bed/{filename}'.format(filename=config['annotations']['filename'])
	run:
		path=config['annotations']['path']
		shell('wget {path} -O {output}.zip')
		shell('unzip {output}.zip -d genes/bed/')

# convert gff to bed
rule gff_to_bed:
	input:
		'genes/bed/{filename}'.format(filename=config['annotations']['filename'])
	output:
		'genes/bed/all-genes.bed'
	shell:
		"awk \'($3 == \"gene\"){{ printf(\"%s\\t%d\\t%d\\n\", $1,$4,$5) }} \' {input} | sed \"s/ST4.03ch/chr/g\" | sortBed -i - > {output}"

# split bed by chromosome
rule split_bed_by_chromosome:
	input:
		'genes/bed/all-genes.bed'
	output:
		'genes/regions/all-genes.{chrom}.regions'
	shell:
                "mergeBed -i {input} | awk \'($1 == \"{wildcards.chrom}\") {{printf(\"%s:%d-%d\\n\", $1,$2,$3)}}\' > {output}"

# call variants based on corrected reads
rule call_variants:
	input:
		bam=alignments + 'qualities/correctedminion.{chrom}-qualities.bam',
		bai=alignments + 'qualities/correctedminion.{chrom}-qualities.bam.bai',
		fasta=reference,
		fai=reference + '.fai',
		regions='genes/regions/all-genes.{chrom}.regions'
	output:
		'vcf/variants.{chrom}.vcf'
	log:
		'vcf/variants.{chrom}.log'
	benchmark:
		'vcf/variants.{chrom}.rsrc'
	params:
		timeout = 2700, # time in seconds
		scripts = config['scripts']
	threads: 16
	shell:
		'{params.scripts}/fb-parallel-timeout.sh {input.regions} {threads} {params.timeout} {log}' \
		' -p 4 --no-indels --no-mnps --no-complex  -f {input.fasta} {input.bam} > {output}'

# run polyphase
rule polyphase:
	input:
		variants='vcf/variants.{chrom}.vcf',
		bam=alignments + '{technology}.{chrom}.bam',
		reference=reference
	output:
		'phasing/polyphase/{technology}.{chrom}.polyphase-{mode}.vcf.gz'
	log:
		'phasing/polyphase/{technology}.{chrom}.polyphase-{mode}.log'	
	run:
		validate = '--verify-genotypes' if wildcards.mode == 'verified-genotypes' else ''
		shell('(time {whatshap} polyphase --ignore-read-groups {validate} -o phasing/polyphase/{wildcards.technology}.{wildcards.chrom}.polyphase-{wildcards.mode}.vcf --reference {input.reference} {input.variants} {input.bam} 4) &> {log}')
		shell('bgzip phasing/polyphase/{wildcards.technology}.{wildcards.chrom}.polyphase-{wildcards.mode}.vcf')
		shell('tabix -p vcf {output}')


####### evaluate phasing of potato genes #######

# merge all phased vcfs into one (genome wide)
rule merge_phasing_vcfs:
	input:
		expand('phasing/polyphase/{{technology}}.{chrom}.polyphase-{{mode}}.vcf.gz', chrom=chromosomes)
	output:
		'genes/polyphase/{technology}.polyphase-{mode}.vcf.gz'
	run:
		input_files=" ".join([x for x in input])
		shell('rtg vcfmerge -o {output} {input_files}')

# get statistics
rule gene_stats:
	input:
		vcf='genes/polyphase/{technology}.polyphase-{mode}.vcf.gz',
		bed='genes/bed/all-genes.bed'
	output:
		tsv='genes/stats/{technology}.polyphase-{mode}.tsv',
		blocks='genes/stats/{technology}.polyphase-{mode}.blocks'
	log:
		'genes/stats/{technology}.polyphase-{mode}.log'
	shell:
		'{whatshap} stats --tsv {output.tsv} --block-list {output.blocks} --bed {input.bed} 4 {input.vcf} > {log} 2>&1'

# plot histogram of block length
rule plot_gene_block_numbers:
	input:
		'genes/stats/{technology}.polyphase-{mode}.tsv'
	output:
		blocks='genes/plots/{technology}.polyphase-{mode}-block_lengths.pdf',
		longest='genes/plots/{technology}.polyphase-{mode}-longest_block.pdf'
	log:
		'genes/plots/{technology}.polyphase-{mode}.log'
	params:
		scripts=config['scripts']
	shell:
		'python3 {params.scripts}/plot-phasing-histograms.py {input} --only-intervals -o genes/plots/{wildcards.technology}.polyphase-{wildcards.mode} > {log}'


####### tag the reads according to the phasing #######

# add read groups (version of haplotag in polyploid branch still requires read groups)
# additionally, fix readnames as corrected-reads fasta constains duplicate readnames
rule add_readgroups:
	input:
		bam=alignments + '{technology}.{chrom}.bam',
		bai=alignments + '{technology}.{chrom}.bam.bai'
	output:
		'haplotag/alignments-with-readgroups/{technology}.rg.{chrom}.bam'
	shell:
		'samtools view -h {input.bam} | python3 ../../scripts/fix_readnames.py | picard AddOrReplaceReadGroups I=/dev/stdin O={output} VALIDATION_STRINGENCY=LENIENT RGID=sample RGLB=library RGPL=nanopore RGPU=platform RGSM=unknown'

# run haplotag
rule haplotag:
	input:
		bam='haplotag/alignments-with-readgroups/{technology}.rg.{chrom}.bam',
		bai='haplotag/alignments-with-readgroups/{technology}.rg.{chrom}.bam.bai',
		vcf='phasing/polyphase/{technology}.{chrom}.polyphase-{mode}.vcf.gz',
		fasta=reference
	output:
		'haplotag/tagged/{technology}.{mode}.tagged.{chrom}.bam'
	params:
		ploidy=4
	shell:
		'{whatshap} haplotag {input.vcf} {input.bam} {params.ploidy} -o {output}'


# annotate genes
rule snpeff_annotate:
	input:
		'genes/polyphase/{technology}.polyphase-{mode}.vcf.gz'
	output:
		'genes/annotation/{technology}.polyphase-{mode}_annotated.vcf'
	shell:
		'java -Xmx4g -jar {snpeff} -v potato {input} > {output}'


######## Assembly of Frigida gene #########

rule tag_reads_of_longest_block:
	input:
		vcf='phasing/polyphase/{technology}.{chrom}.polyphase-{mode}.vcf.gz',
		bam='haplotag/alignments-with-readgroups/{technology}.rg.{chrom}.bam',
		reference=reference
	output:
		vcf='assembly/polyphase/{technology}.{chrom}.polyphase-{mode}-frigida.vcf.gz',
		tsv='assembly/haplotag/{technology}.{mode}.tagged.{chrom}-frigida.tsv',
		bam='assembly/haplotag/{technology}.{mode}.tagged.{chrom}-frigida.bam'
	params:
		region="chr04:61677628-61680091",
		ploidy=4
	log:
		'assembly/haplotag/{technology}.{mode}.tagged.{chrom}-frigida.log'
	run:
		shell("bcftools view {input.vcf} {params.region} | python3 ../../scripts/largest-block-in-region.py {params.region} | bgzip > {output.vcf}")
		shell("tabix -p vcf {output.vcf}")
		shell("{whatshap_haplotag} haplotag {output.vcf} {input.bam} --reference {input.reference}  --ploidy {params.ploidy} --output-haplotag-list {output.tsv} -o {output.bam} &> {log}")


rule split_bam:
	input:
		bam='haplotag/alignments-with-readgroups/{technology}.rg.{chrom}.bam',
		tsv='assembly/haplotag/{technology}.{mode}.tagged.{chrom}-frigida.tsv'
	output:
		bamh1='assembly/split/{technology}.{mode}.{chrom}.h1.bam',
		bamh2='assembly/split/{technology}.{mode}.{chrom}.h2.bam',
		bamh3='assembly/split/{technology}.{mode}.{chrom}.h3.bam',
		bamh4='assembly/split/{technology}.{mode}.{chrom}.h4.bam',
	log:
		'assembly/split/{technology}.{mode}.{chrom}.log'
	shell:
		"{whatshap_haplotag} split {input.bam} {input.tsv} --output-h1 {output.bamh1} --output-h2 {output.bamh2} --output-h3 {output.bamh3} --output-h4 {output.bamh4} &> {log}"

rule assemble_haplotype:
	input:
		'assembly/split/{technology}.{mode}.{chrom}.h{hap}.bam'
	output:
		'assembly/contigs/{technology}.{mode}.{chrom}.wtdbg2-h{hap}.raw.fa'
	run:
		shell("samtools fastq {input} > assembly/contigs/h{wildcards.hap}.fastq")
		shell("wtdbg2 -x ccs -g 1m -t 4 -i assembly/contigs/h{wildcards.hap}.fastq -fo assembly/contigs/wtdbg2-h{wildcards.hap}")
		shell("wtpoa-cns -i assembly/contigs/wtdbg2-h{wildcards.hap}.ctg.lay.gz -fo {output}")
		
rule align_contigs:
	input:
		contigs='assembly/contigs/{technology}.{mode}.{chrom}.wtdbg2-h{hap}.raw.fa',
		reference=reference
	output:
		'assembly/contigs/{technology}.{mode}.{chrom}.wtdbg2-h{hap}.raw.bam'
	threads: 30
	shell:
		"minimap2 -ax asm5 --cs -t {threads} {input.reference} {input.contigs} | samtools view -bS - | samtools sort -o {output}"

